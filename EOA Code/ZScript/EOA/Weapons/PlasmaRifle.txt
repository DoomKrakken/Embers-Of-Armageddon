Class PlasmaRifleSelected : EOA_Token {}

//Heat Blast - Collects extra heat from plasma projectiles that can be released as a powerful shockwave.  WARNING: Plasma gun won't fire while cooling off from a Heat Blast.
Class HeatBlast : EOA_Token {}
Class HeatBlastSelected : EOA_Token {}
Class SuperHeatedRounds : EOA_Token {} //Rounds build heat much faster.
Class ImprovedVenting : EOA_Token {} //Faster Cooldown.
Class ExpandedThreshold : EOA_Token {} //More heat capacity, which results in higher damage potential.

Class MC_HeatedCore : EOA_MasteryChallengeToken //Kill multiple enemies with a single Heat Blast 20 times.
{
	Default
	{
		EOA_MasteryChallengeToken.MasteryItem "HeatedCore";
		EOA_MasteryChallengeToken.ChallengeCounter "HeatedCoreMasteryCounter";
		Tag "Heated Core";
	}
}

Class HeatedCore : EOA_Token {} //Mastery Upgrade - Heat builds automatically and passively, without needing to use ammo.

Class HeatBlastCharged : EOA_Token {}
Class HeatBlastReady : EOA_Token {}
Class HeatBlastChargeCounter : EOA_Token {Default {Inventory.MaxAmount 140;} }
Class HeatBlastCooldownCounter : EOA_Token {Default {Inventory.MaxAmount 60;} }
Class HeatBlastSubChargeCounter : EOA_Token {Default {Inventory.MaxAmount 7;} } //For Heated Core.
Class HeatedCoreKillCounter : EOA_Token {Default {Inventory.MaxAmount 2;} }
Class HeatedCoreMasteryCounter : EOA_Token {Default {Inventory.MaxAmount 20;} }


//Stun Bomb - Stuns enemies in a large radius.
Class StunBomb : EOA_Token {}
Class StunBombSelected : EOA_Token {}
Class QuickRecharge : EOA_Token {} //Faster Cooldown.
Class BigShock : EOA_Token {} //Larger Blast Radius
Class LongerStun : EOA_Token {} //Longer Stun Duration.

Class MC_ChainStun : EOA_MasteryChallengeToken //Kill 30 stunned enemies.
{
	Default
	{
		EOA_MasteryChallengeToken.MasteryItem "ChainStun";
		EOA_MasteryChallengeToken.ChallengeCounter "ChainStunMasteryCounter";
		Tag "Chain Stun";
	}
}

Class ChainStun : EOA_Token {} //Mastery Upgrade - Enemies killed while stunned will stun enemies around them.

Class StunBombCharged : EOA_Token {}
Class StunBombReady : EOA_Token {}
Class StunBombCooldownCounter : EOA_Token {Default {Inventory.MaxAmount 350;} }
Class ChainStunMasteryCounter : EOA_Token {Default {Inventory.MaxAmount 30;} }

Class PLRMods : EOA_Manipulator
{
	States
	{
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("HeatBlast");
			A_GiveInventory("StunBomb");
		}
		Stop;
	}
}

Class PLRUpgrades : EOA_Manipulator
{
	States
	{
	Pickup:
		TNT1 A 0
		{
			A_GiveInventory("SuperHeatedRounds");
			A_GiveInventory("ImprovedVenting");
			A_GiveInventory("ExpandedThreshold");
			A_GiveInventory("QuickRecharge");
			A_GiveInventory("BigShock");
			A_GiveInventory("LongerStun");
		}
		Stop;
	}
}

Class EOA_PlasmaRifle : EOA_Weapon
{
	Default
	{
		Inventory.Icon "ICONPLR";
		Weapon.AmmoType "EOA_CellAmmo";
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 120;
		Weapon.SlotNumber 6;
		Inventory.PickupMessage "Plasma Rifle";
		Tag "Plasma Rifle";
	}
	Override Void MarkPrecacheSounds()
	{
		MarkSound("PLR/Fire1");
		MarkSound("PLR/Fire2");
		MarkSound("PLR/Fire3");
		MarkSound("PLR/Fire4");
		MarkSound("PLR/Base");
		MarkSound("PLR/Spin");
		MarkSound("PLR/HeatAdd");
		MarkSound("HeatBlast/Charged");
		MarkSound("HeatBlast/Equip");
		MarkSound("StunBomb/Charged");
		MarkSound("StunBomb/Equip");
		Super.MarkPrecacheSounds();
	}
	Override Void DoEffect()
	{
		Let Player = Owner.Player;
		If (!Player)
		{
			Super.DoEffect();
			Return;
		}

		//Mod not equipped...
		If ((Owner.GetPlayerInput(INPUT_BUTTONS) & BT_ALTATTACK) && !(Owner.GetPlayerInput(INPUT_OLDBUTTONS) & BT_ALTATTACK) && Player.ReadyWeapon == Self && !(Owner.CountInv("HeatBlastSelected") || Owner.CountInv("StunBombSelected")))
		{
			EOA_SpawnNoiseMaker("Weapon/DryFire");
		}

		//Cooldown
		EOA_Token.SetMaxInventory(Owner, "HeatBlastChargeCounter", 140 + 100 * Owner.CountInv("ExpandedThreshold"));
		If (Owner.CountInv("HeatBlastSelected"))
		{
			Owner.A_TakeInventory("HeatBlastCooldownCounter", (1 + 2 * Owner.CountInv("ImprovedVenting")) * (1 + Owner.CountInv("PowerHaste")));
		}
		If (Owner.CountInv("StunBombSelected"))
		{
			Owner.A_TakeInventory("StunBombCooldownCounter", (1 + Owner.CountInv("QuickRecharge")) * (1 + Owner.CountInv("PowerHaste")));
		}
		Inventory HBCounter = Inventory(Owner.FindInventory("HeatBlastChargeCounter"));
		If (HBCounter && Owner.CountInv("HeatBlastChargeCounter") >= HBCounter.MaxAmount)
		{
			If (!Owner.CountInv("HeatBlastReady"))
			{
				Owner.A_GiveInventory("HeatBlastCharged");
			}
		}
		Else
		{
			Owner.A_TakeInventory("HeatBlastReady");
			Owner.A_TakeInventory("HeatBlastCharged");
		}
		If (!Owner.CountInv("StunBombCooldownCounter") && !Owner.CountInv("StunBombReady"))
		{
			Owner.A_GiveInventory("StunBombCharged");
		}
		If (Owner.CountInv("HeatedCore") && !Owner.CountInv("HeatBlastCooldownCounter"))
		{
			Owner.A_GiveInventory("HeatBlastSubChargeCounter", 1 + Owner.CountInv("PowerHaste"));
			If (Owner.CountInv("HeatBlastSubChargeCounter") >= 3)
			{
				Owner.A_TakeInventory("HeatBlastSubChargeCounter");
				Owner.A_GiveInventory("HeatBlastChargeCounter", 2);
			}
		}
		If (!Owner.CountInv("HeatBlastSelected"))
		{
			Owner.A_TakeInventory("HeatBlastSubChargeCounter");
			Owner.A_TakeInventory("HeatBlastChargeCounter");
		}
		
		//Mastery - Heated Core (Heat Blast)
		If (!Owner.CountInv("HeatedCore") && Owner.CountInv("SuperHeatedRounds") && Owner.CountInv("ImprovedVenting") && Owner.CountInv("ExpandedThreshold"))
		{
			Owner.A_GiveInventory("MC_HeatedCore");
			If (Owner.CountInv("HeatedCoreMasteryCounter") >= 20)
			{
				Owner.A_GiveInventory("HeatedCore");
				Owner.A_TakeInventory("HeatedCoreMasteryCounter");
			}
		}
		Else
		{
			Owner.A_TakeInventory("MC_HeatedCore");
		}
		
		//Mastery - Chain Stun (Stun Bomb)
		If (!Owner.CountInv("ChainStun") && Owner.CountInv("QuickRecharge") && Owner.CountInv("BigShock") && Owner.CountInv("LongerStun"))
		{
			Owner.A_GiveInventory("MC_ChainStun");
			If (Owner.CountInv("ChainStunMasteryCounter") >= 30)
			{
				Owner.A_GiveInventory("ChainStun");
				Owner.A_TakeInventory("ChainStunMasteryCounter");
			}
		}
		Else
		{
			Owner.A_TakeInventory("MC_ChainStun");
		}
		Super.DoEffect();
	}
	Action Void EOA_HeatBlast(Int DistThresh, Double AngThresh) //[DoomKrakken]: Derived from A_Blast.  Modified to only work within a certain angle. :D
	{
		Bool BlastCheck;
		Actor BlastWave;
		[BlastCheck, BlastWave] = A_SpawnItemEx("HeatBlastWave", 0, 0, Height / 2, Flags: SXF_TRANSFERPITCH|SXF_SETTARGET);
		If (BlastWave)
		{
			//[DoomKrakken]: Spawn four more in the same fashion, for better brightness. :D
			A_SpawnItemEx("HeatBlastWave", 0, 0, Height / 2, Flags: SXF_TRANSFERPITCH);
			A_SpawnItemEx("HeatBlastWave", 0, 0, Height / 2, Flags: SXF_TRANSFERPITCH);
			A_SpawnItemEx("HeatBlastWave", 0, 0, Height / 2, Flags: SXF_TRANSFERPITCH);
			A_SpawnItemEx("HeatBlastWave", 0, 0, Height / 2, Flags: SXF_TRANSFERPITCH);
		}
		BlockThingsIterator It = BlockThingsIterator.Create(Self, DistThresh);
		//Actor Mo;
		While (It.Next()) //(Mo = Actor(It.Next()))
		{
			Actor Mo = It.Thing;
			//Check for the Heat Blast Wave
			If (!BlastWave || !Mo || Mo == Self || !Mo.bSHOOTABLE || !CheckIfCloser(Mo, DistThresh))
			{
				Continue;
			}
			//Gotta be somewhat in front of you.  Remember, this heat blast isn't a fart... (thank you, Kodi, Xangi, and Gutawer!)
			Vector3 PosDif = Vec3To(Mo); //Difference in position between inflictor and victim.
			Double RelAngle = AbsAngle(Angle, atan2(PosDif.Y, PosDif.X)); //Difference in angle between inflictor and victim.
			If (RelAngle >= AngThresh)
			{
				Continue;
			}
			If (Mo.CurSector.PortalGroup != CurSector.PortalGroup || !CheckSight(Mo) || Mo.bKILLED)
			{
				Continue;
			}
			Mo.DamageMobj(BlastWave, Self, CountInv("HeatBlastChargeCounter"), "Normal", DMG_EXPLOSION);
			If (Mo.bKILLED && Mo.bISMONSTER)
			{
				A_GiveInventory("HeatedCoreKillCounter");
			}
		}
		If (CountInv("MC_HeatedCore") && CountInv("HeatedCoreKillCounter") >= 2)
		{
			A_GiveInventory("HeatedCoreMasteryCounter");
		}
		A_TakeInventory("HeatedCoreKillCounter");
	}
	States
	{
	Spawn:
		PLSR A -1;
		Stop;
	Select:
		TNT1 A 0
		{
			Invoker.SemiAutoOverride = True;
			A_WeaponOffset();
			A_GiveInventory("PlasmaRifleSelected");
			If (GetCVar("EOA_Reticles"))
			{
				ACS_NamedExecuteAlways("PLRReticle", 0);
				A_SetCrosshair(99);
			}
			Else
			{
				A_SetCrosshair(0);
			}
		}
		TNT1 A 0 A_Raise();
		Wait;
	Deselect:
		PRHR A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRSR A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNR A 0;
		"####" "#" 0 EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "", 7);
		"####" HFDB 1;
		TNT1 A 4
		{
			A_TakeInventory("PlasmaRifleSelected");
			EOA_DestroyPersistentNoiseMaker(Invoker.SlotNumber);
			A_SetTics(4 * !CountInv("Adept") * !CountInv("PowerBerserk"));
		}
		TNT1 A 1 A_Lower(36);
		Wait;
	Ready:
		TNT1 A 0
		{
			If (Level.Time && !(CountInv("HeatBlastSwitch") || CountInv("StunBombSwitch")))
			{
				EOA_SpawnNoiseMaker("Weapon/Switch");
			}
		}
		PRHR A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRSR A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNR A 0;
		"####" ACEG 1
		{
			EOA_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_DISABLEHASTE);
			If (CountInv("HeatBlastSwitch"))
			{
				A_TakeInventory("HeatBlastSwitch");
				Return ResolveState("HeatBlastMod");
			}
			Else If (CountInv("StunBombSwitch"))
			{
				A_TakeInventory("StunBombSwitch");
				Return ResolveState("StunBombMod");
			}
			Return ResolveState(Null);
		}
		Goto ReadyLoop;
	ReRaise:
		TNT1 A 0 A_WeaponOffset(-144, 32);
		PRHR A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRSR A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNR A 0;
		"####" ABDEGH 1
		{
			EOA_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_DISABLEHASTE);
			A_WeaponOffset(24, 0, WOF_KEEPY|WOF_ADD);
		}
	ReadyLoop:
		TNT1 A 0
		{
			If (CountInv("HeatBlastCharged") && CountInv("HeatBlastSelected"))
			{
				EOA_SpawnNoiseMaker("HeatBlast/Charged");
				A_GiveInventory("HeatBlastReady");
				A_TakeInventory("HeatBlastCharged");
			}
			If (CountInv("StunBombCharged") && CountInv("StunBombSelected"))
			{
				EOA_SpawnNoiseMaker("StunBomb/Charged");
				A_GiveInventory("StunBombReady");
				A_TakeInventory("StunBombCharged");
			}
		}
		PRHI A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRSI A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNI A 0;
		"####" "#" 1 
		{
			EOA_WeaponReady(WRF_NOSWITCH * Invoker.SemiAutoOverride|WRF_NOPRIMARY * (CountInv("HeatBlastSelected") && CountInv("HeatBlastCooldownCounter"))|WRF_NOSECONDARY * (CountInv("StunBombSelected") && CountInv("StunBombCooldownCounter"))|WRF_ALLOWRELOAD|WRF_ALLOWZOOM * !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ZOOM), EOA_ALLOWMANTLE|EOA_ALLOWEQUIPMENT);
			Invoker.SemiAutoOverride = False;
			If (CountInv("HeatBlastSwitch"))
			{
				A_TakeInventory("HeatBlastSwitch");
				Return ResolveState("SwitchToHeatBlast");
			}
			Else If (CountInv("StunBombSwitch"))
			{
				A_TakeInventory("StunBombSwitch");
				Return ResolveState("SwitchToStunBomb");
			}
			Return ResolveState(Null);
		}
		Loop;
	Fire:
		PRHF A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRSF A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNF A 0;
		"####" "#" 0 
		{
			EOA_FireBullets(0, 0, -1, 0, "EOA_NullPuff", FBF_NORANDOMPUFFZ|FBF_USEAMMO, 32767, "EOA_PlasmaBall", random(-6, -4) + GetViewZ(), random(7, 10) + GetRelVelY() / 4);
			EOA_SetWeaponRecoilOffset(frandompick(-0.5, 0.5), frandompick(-0.5, 0.5), 0, 0.25);
			EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PulseLight, (25, 204, 255), 48, 1, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, 3, (0, 0, Height / 2), 0.1);
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Fire1", 1, 0, 0.5);
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Fire2", 2, 0, 0.25);
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Fire3", 3, 0, 0.25);
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Fire4", 4);
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Base", 5);
			If (CountInv("HeatBlastSelected"))
			{
				If (CountInv("SuperHeatedRounds"))
				{
					EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/HeatAdd", 7, CHANF_LOOPING);
				}
				A_GiveInventory("HeatBlastChargeCounter", 4 * (1 + CountInv("SuperHeatedRounds")));
			}
			If (CountInv("PowerQDMG"))
			{
				EOA_SpawnNoiseMaker("QuadDamage/Shot");
			}
			A_AlertMonsters();
		}
		"####" ABC 1
		{
			EOA_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY * !(((CountInv("HeatBlastSelected") && !CountInv("HeatBlastCooldownCounter") && CountInv("HeatBlastChargeCounter") >= 12 * (1 + CountInv("ExpandedThreshold"))) || (CountInv("StunBombSelected") && !CountInv("StunBombCooldownCounter"))) && (GetPlayerInput(INPUT_BUTTONS) & BT_ALTATTACK) && !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ALTATTACK))|WRF_ALLOWZOOM * !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ZOOM), EOA_ALLOWMANTLE|EOA_ALLOWEQUIPMENT);
			If (CountInv("HeatBlastCharged") && CountInv("HeatBlastSelected"))
			{
				EOA_SpawnNoiseMaker("HeatBlast/Charged");
				A_GiveInventory("HeatBlastReady");
				A_TakeInventory("HeatBlastCharged");
			}
			If (CountInv("StunBombCharged") && CountInv("StunBombSelected"))
			{
				EOA_SpawnNoiseMaker("StunBomb/Charged");
				A_GiveInventory("StunBombReady");
				A_TakeInventory("StunBombCharged");
			}
			Return ResolveState(Null);
		}
		"####" D 1 A_ReFire();
		"####" "#" 0 EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "PLR/Spin", 7, 0, 0.5);
		Goto ReadyLoop;
	AltFire:
		TNT1 A 0 A_JumpIf((GetPlayerInput(INPUT_BUTTONS) & BT_ALTATTACK) && !(GetPlayerInput(INPUT_OLDBUTTONS) & BT_ALTATTACK), 1);
		Goto ReadyLoop;
		TNT1 A 0 A_JumpIf(CountInv("HeatBlastSelected") && CountInv("HeatBlastChargeCounter") >= 12 * (1 + CountInv("ExpandedThreshold")) && !CountInv("HeatBlastCooldownCounter"), "HeatBlastLaunch");
		TNT1 A 0 A_JumpIf(CountInv("StunBombSelected") && !CountInv("StunBombCooldownCounter") && (CountInv("EOA_CellAmmo") >= 10 || CheckInfiniteAmmo()), "StunBombLaunch");
		Goto ReadyLoop;
	HeatBlastLaunch:
		TNT1 A 0
		{
			EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "", 7);
			EOA_HeatBlast(512, 45);
			A_AlertMonsters();
			A_SpawnItemEx("HeatBlastSFX", Flags: SXF_TRANSFERPOINTERS|SXF_SETTARGET|SXF_NOCHECKPOSITION);
			A_TakeInventory("HeatBlastReady");
			A_TakeInventory("HeatBlastChargeCounter");
			A_GiveInventory("HeatBlastCooldownCounter", 60);
			EOA_SetWeaponRecoilOffset(frandompick(-1, -0.5, 0.5, 1), -5, 0, 0.25);
			EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PulseLight, (25, 204, 255), 48, 1, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, 3, (0, 0, Height / 2), 0.1);
			If (CountInv("PowerQDMG"))
			{
				EOA_SpawnNoiseMaker("QuadDamage/Shot");
				EOA_SpawnNoiseMaker("QuadDamage/Shot");
				EOA_SpawnNoiseMaker("QuadDamage/Shot");
			}
		}
		PRHB ABCDEFGHI 1 
		{
			EOA_WeaponReady(WRF_NOFIRE, EOA_ALLOWMANTLE);
			If (!CountInv("HeatBlastSelected"))
			{
				Return ResolveState("ReadyLoop");
			}
			Return ResolveState(Null);
		}
		PRHB JKLMNOP 1 
		{
			EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD|WRF_ALLOWZOOM, EOA_ALLOWMANTLE|EOA_ALLOWEQUIPMENT);
			If (!CountInv("HeatBlastSelected"))
			{
				Return ResolveState("ReadyLoop");
			}
			Return ResolveState(Null);
		}
		Goto ReadyLoop;
	StunBombLaunch:
		TNT1 A 0
		{
			A_FireProjectile("StunBombProjectile", 0, 0, 10, 2 + GetViewZ());
			A_SpawnItemEx("StunBombSFX", Flags: SXF_TRANSFERPOINTERS|SXF_SETTARGET|SXF_NOCHECKPOSITION);
			EOA_SetWeaponRecoilOffset(frandompick(-1, -0.5, 0.5, 1), -6, 0, 0.25);
			EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PulseLight, (25, 204, 255), 48, 1, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, 3, (0, 0, Height / 2), 0.1);
			A_TakeInventory("EOA_CellAmmo", 10, TIF_NOTAKEINFINITE);
			A_TakeInventory("StunBombReady");
			A_GiveInventory("StunBombCooldownCounter", 350);
			If (CountInv("PowerQDMG"))
			{
				EOA_SpawnNoiseMaker("QuadDamage/Shot");
			}
		}
		PRSB ABCDDDDDDDEFGHIJ 1
		{
			EOA_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD|WRF_ALLOWZOOM, EOA_ALLOWMANTLE|EOA_ALLOWEQUIPMENT);
			If (!CountInv("StunBombSelected"))
			{
				Return ResolveState("ReadyLoop");
			}
			Return ResolveState(Null);
		}
		Goto ReadyLoop;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("HeatBlast") && !CountInv("HeatBlastSelected"), "SwitchToHeatBlast");
		TNT1 A 0 A_JumpIf(CountInv("StunBomb") && !CountInv("StunBombSelected"), "SwitchToStunBomb");
		Goto ReadyLoop;
	SwitchToHeatBlast:
		PRSR A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNR A 0;
		"####" HGFEDCBA 1;
	HeatBlastMod:
		TNT1 A 0
		{
			A_GiveInventory("HeatBlastSelected");
			A_TakeInventory("StunBombSelected");
			EOA_SpawnNoiseMaker("HeatBlast/Equip");
			If (CountInv("HeatBlastCharged") && CountInv("HeatBlastSelected"))
			{
				EOA_SpawnNoiseMaker("HeatBlast/Charged");
				A_GiveInventory("HeatBlastReady");
				A_TakeInventory("HeatBlastCharged");
			}
		}
		PRHM ABCDEFGHIJKLMNOPQ 1 EOA_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		PRHM RRRRRRRRRRRRRRRRRRRRRRRRRR 1 EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD * CountInv("StunBomb"), EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		PRHM SSTT 1 EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD * CountInv("StunBomb"), EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		Goto ReadyLoop;
	SwitchToStunBomb:
		PRHR A 0 A_JumpIfInventory("HeatBlastSelected", 1, 2);
		PRNR A 0;
		"####" HGFEDCBA 1;
	StunBombMod:
		TNT1 A 0 
		{
			A_GiveInventory("StunBombSelected");
			A_TakeInventory("HeatBlastChargeCounter");
			A_TakeInventory("HeatBlastSelected");
			A_TakeInventory("HeatBlastReady");
			EOA_SpawnNoiseMaker("StunBomb/Equip");
		}
		PRSM ABCDEFGHIJKLMNOPQ 1 EOA_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH, EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		PRSM RRRRRRRRRRR 1 EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD * CountInv("HeatBlast"), EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		PRSM SSSSSSSSSSS 1 EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD * CountInv("HeatBlast"), EOA_ALLOWMANTLE|EOA_WEAPSPEEDUPGRADE * CountInv("HotSwap"));
		PRSM TUVW 1 EOA_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD * CountInv("HeatBlast"), EOA_ALLOWMANTLE);
		Goto ReadyLoop;
	Zoom:
		PRHT A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRST A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNT A 0;
		"####" "#" 0 EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "", 7);
		Goto Super::Melee;
	LedgeClimb:
		"####" "#" 0 EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "", 7);
		Goto Super::LedgeClimb;
	UseEquipment:
		PRHT A 0 A_JumpIfInventory("HeatBlastSelected", 1, 3);
		PRST A 0 A_JumpIfInventory("StunBombSelected", 1, 2);
		PRNT A 0;
		"####" "#" 0 EOA_SpawnPersistentNoiseMaker(Invoker.SlotNumber, "", 7);
		Goto Super::UseEquipment;
	}
}

Class EOA_PlasmaBall : EOA_Projectile //Many sources use these sprites... my source was ZION.
{
	Default
	{
		Radius 4;
		Height 8;
		DamageFunction 10;
		Speed 50;
		RenderStyle "Add";
		Alpha 0.99;
		Scale 0.075;
		Decal "EMGPuffBigScorch";
		Translation "0:255=%[0, 0, 0]:[0.7, 0.9, 2]";
		EOA_Projectile.PuffType "EOA_PlasmaPuffComplex";
		+BRIGHT
		+FORCERADIUSDMG
		+FORCEXYBILLBOARD
		+ROLLCENTER
		+ROLLSPRITE
	}
	Override Void PostBeginPlay()
	{
		A_SetRoll(random(0, 359));
		A_StartSound("PlasmaBall/Fly", 1, CHAN_LOOP, 0.5);
		A_StartSound("PlasmaBall/Add1", 2, CHAN_LOOP, 0.25);
		A_StartSound("PlasmaBall/Add2", 3, CHAN_LOOP, 0.25);
		Super.PostBeginPlay();
	}
	States
	{
	Spawn:
		PSB1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PointLight, (25, 204, 255), 16, 16, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, Tics, (0, 0, Height / 2));
		PSB2 ABCDEF 1 EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PointLight, (25, 204, 255), 16, 16, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, Tics, (0, 0, Height / 2));
		Loop;
	XDeath:
		TNT1 A 0 A_StartSound("Projectile/HitFlesh");
	Crash:
	Death:
		TNT1 A 1
		{
			A_Explode(4 * (1 - (Self.GetClassName() == "DecoyPlasmaBall")), 48, 0, 1, 48);
			A_SpawnItemEx("EOA_PlasmaPuffComplex");
			A_StartSound("PlasmaBall/Impact", 5, 0, 0.25);
		}
		Stop;
	}
}

Class DecoyPlasmaBall : EOA_PlasmaBall
{
	Default
	{
		Decal "";
		DamageFunction random(0, 1);
		+PAINLESS
	}
}

Class EOA_PlasmaPuff : Actor//Graphics taken from D4T and ZION. :D
{
	Default
	{
		Radius 1;
		Height 1;
		RenderStyle "Add";
		Scale 0.25;
		Alpha 0.99;
		Translation "0:255=%[0, 0, 0]:[0.6, 1, 1.8]";
		+BRIGHT
		+DONTBLAST
		+DONTTHRUST
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+ROLLCENTER
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
		DBP2 A 0 A_Jump(128, 2);
		DBPP A 0;
		"####" ABCDEFGHI 1;
		Stop;
	}
}

Class EOA_PlasmaPuffComplex : EOA_PlasmaPuff
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_SpawnItemEx("PlasmaShockwave", Flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("EOA_PlasmaPuff", Flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("PlasmaImpactLightning", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), Flags: SXF_NOCHECKPOSITION);
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "PLRAddLight", DynamicLight.PulseLight, (25, 204, 255), 32, 1, DYNAMICLIGHT.LF_ADDITIVE, 3, (0, 0, Height / 2), 1.0 * 3 / TICRATE);
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "PLRAttLight", DynamicLight.PulseLight, (25, 204, 255), 32, 1, DYNAMICLIGHT.LF_ATTENUATE, 3, (0, 0, Height / 2), 1.0 * 3 / TICRATE);
		}
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnProjectile("PlasmaSpark", 0, 0, random(1, 360), CMF_AIMDIRECTION, random(-45, 45));
			A_SpawnProjectile("ChaingunUraniumPuffSpark", 0, 0, random(1, 360), CMF_AIMDIRECTION, random(-90, 45));
		}
		Goto Super::Spawn;
	}
}

//[DoomKrakken]: Effects pilfered from ZION... :D
Class PlasmaShockWave : Actor
{
	Default
	{
		Scale 0.33;
		RenderStyle "Add";
		Alpha 0.99;
		Translation "0:255=%[0, 0, 0]:[1.07, 1.65, 2]"; //"0:255=%[0, 0, 0]:[0, 0.5, 1.5]";
		+BRIGHT
		+DONTSPLASH
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+NOTRIGGER
		+ROLLCENTER
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		PLSK A 0 NoDelay A_SetRoll(frandom(0, 359));
		PLSK AAA 1 
		{
			A_FadeOut(0.2);
			A_SetScale(Scale.X * 1.2);
		}
		Stop;
	}
}

Class PlasmaImpactLightning : Actor
{
	Default
	{
		RenderStyle "Add";
		Translation "0:255=%[0, 0, 0]:[1.07, 1.65, 2]";
		Scale 0.075;
		Alpha 0.99;
		+BRIGHT
		+DONTSPLASH
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+NOTRIGGER
		+ROLLCENTER
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			A_SetRoll(random(0, 359));
			Return A_Jump(256, random(1, 8));
		}
		LBLT ABCDEFGH 0 A_Jump(256, "KeepSprite");
	KeepSprite:
		LBLT "#" 1 
		{
			A_FadeOut(0.25);
			A_SetScale(Scale.X * 1.3);
		}
		Wait;
	}
}

Class PlasmaSpark : Actor
{
	Default
	{
		Radius 0;
		Height 0;
		Speed 7;
		RenderStyle "Add";
		Translation "0:255=%[0, 0, 0]:[1.07, 1.65, 2]";
		Scale 0.2;
		+BRIGHT
		+CANNOTPUSH
		+DONTSPLASH
		+FORCEXYBILLBOARD
		+MISSILE
		+NOBLOCKMAP
		+NOBLOCKMONST
		+NOTRIGGER
		+THRUACTORS
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, random(1, 4));
		DEBR ABCD 0 A_Jump(256, "FX");
	FX:
		"####" "#" 3 
		{
			A_SetScale(Scale.X * frandom(1, 1.25));
			A_SetRoll(random(0, 359));
		}
		"####" "#" 1
		{
			A_SetScale(Scale.X - 0.025);
			If (Scale.X <= 0)
			{
				Return ResolveState("Null");
			}
			Return ResolveState(Null);
		}
		Wait;
	}
}

Class HeatBlastSFX : Actor
{
	Default
	{
		ReactionTime 70;
		+DONTBLAST
		+DONTTHRUST
		+NOINTERACTION
		+NOTIMEFREEZE
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_StartSound("HeatBlast/Fire", 1);
			A_StartSound("HeatBlast/Fire", 2);
			A_StartSound("HeatBlast/Fire", 3);
			A_StartSound("HeatBlast/Fire", 4);
			A_StartSound("HeatBlast/Shrill", 5, 0, 0.25);
			A_StartSound("HeatBlast/Distort", 6, 0, 0.5);
			If (CountInv("ExpandedThreshold", AAPTR_TARGET))
			{
				A_StartSound("HeatBlast/Add", 7);
			}
		}
		TNT1 A 1
		{
			A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE|WARPF_STOP, Null, 0.5);
			If (ReactionTime-- <= 0 || !Target)
			{
				Return ResolveState("Null");
			}
			Return ResolveState(Null);
		}
		Wait;
	}
}

Class StunBombSFX : HeatBlastSFX
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_StartSound("PRS/Fire", 1);
			A_StartSound("PRS/Add", 2, 0, 0.5);
		}
		Goto Super::Spawn+1;
	}
}

Class HeatBlastWave : Actor //Sprite modified from Guncaster.
{
	Default
	{
		Projectile;
		Renderstyle "Add";
		Alpha 0.99;
		Scale 0.3;
		+BRIGHT
		+DONTBLAST
		+DONTTHRUST
		+FLATSPRITE
		+NOINTERACTION
	}
	States
	{
	Spawn:
		HTWV A 1 NoDelay
		{
			A_FadeOut(0.10);
			A_SetScale(Scale.X + 0.4);
		}
		Wait;
	}
}

Class StunBombProjectile : EOA_Projectile
{
	Default
	{
		Radius 10;
		Height 10;
		Speed 40;
		Scale 0.25;
		RenderStyle "Add";
		Gravity 0.5;
		Alpha 0.99;
		+BRIGHT
		+FORCERADIUSDMG
		+FORCEXYBILLBOARD
		-NOGRAVITY
	}
	Override Int DoSpecialDamage(Actor Target, Int Damage, Name DamageType)
	{
		//[DoomKrakken]: Tanky monsters such as Barons burn through this stuff twice as fast... so the effect tics will be halved.
		If (!Target.bISMONSTER || (Target.bBOSS && Target.Default.Health >= 3000))
		{
			If (!Target.bISMONSTER)
			{
				Return 100;
			}
			Return 0;
		}
		If (Target Is "EOA_WeakPoint")
		{
			EOA_WeakPoint WP = EOA_WeakPoint(Target);
			If (WP && WP.Owner)
			{
				WP.Owner.A_GiveInventory("PowerStunned");
				Let StunItem = PowerStunned(WP.Owner.FindInventory("PowerStunned"));
				If (StunItem)
				{
					StunItem.EffectTics *= 1 + CountInv("LongerStun", AAPTR_TARGET) / (1 + (WP.Owner.Default.Health >= 1000));
					StunItem.EffectTics = clamp(StunItem.EffectTics, 0, 88 * (1 + CountInv("LongerStun", AAPTR_TARGET)) / (1 + (WP.Owner.Default.Health >= 1000))); 
					StunItem.Master = WP.Owner.Target = Self.Target;
				}
			}
		}
		Else
		{
			Target.A_GiveInventory("PowerStunned");
			Let StunItem = PowerStunned(Target.FindInventory("PowerStunned"));
			If (StunItem)
			{
				StunItem.EffectTics *= 1 + CountInv("LongerStun", AAPTR_TARGET) / (1 + (Target.Default.Health >= 1000));
				StunItem.EffectTics = clamp(StunItem.EffectTics, 0, 88 * (1 + CountInv("LongerStun", AAPTR_TARGET)) / (1 + (Target.Default.Health >= 1000)));
				StunItem.Master = Target.Target = Self.Target;
			}
		}
		Return 0;
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, random(1, 3));
		STBM ABC 0 A_Jump(256, "SpawnLoop");
	SpawnLoop:
		"####" "#" 1 EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PointLight, (25, 204, 255), 32, 1, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, Tics, (0, 0, Height / 2));
		Wait;
	Death:
		TNT1 A 10
		{
			A_SpawnItemEx("StunBombProjectileExplosionFX", Flags: SXF_NOCHECKPOSITION);
			A_Explode(1, 48 + (112 * CountInv("BigShock", AAPTR_TARGET)), 0, 1, 48 + (112 * CountInv("BigShock", AAPTR_TARGET)));
			A_StartSound("StunBomb/Impact", 1, 0, 0.5);
			A_StartSound("StunBomb/Explode", 2);
			A_StartSound("StunBomb/Shock", 3, 0, 0.5);
			EOA_SpawnCustomDynamicLight("PLRAddLight", DynamicLight.PulseLight, (25, 204, 255), 96, 1, DYNAMICLIGHT.LF_ADDITIVE, Tics, (0, 0, Height / 2), 1.25 * Tics / TICRATE);
			EOA_SpawnCustomDynamicLight("PLRAttLight", DynamicLight.PulseLight, (25, 204, 255), 96, 1, DYNAMICLIGHT.LF_ATTENUATE, Tics, (0, 0, Height / 2), 1.25 * Tics / TICRATE);
		}
		Stop;
	}
}

Class StunBombProjectileExplosionFX : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		Scale 0.1;
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(0.1 * (1 + CountInv("BigShock", AAPTR_TARGET)));
		TNT1 AAAAA 0 A_SpawnItemEx("StunBombImpactLightning", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), Flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		TNT1 AAA 0 A_SpawnItemEx("StunBombShockWave", Flags: SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		TNT1 A 1;
		Stop;
	}
}

Class StunBombImpactLightning : PlasmaImpactLightning
{
	Default
	{
		Scale 0.2;
		+BRIGHT
	}
	States
	{
	KeepSprite:
		LBLT "#" 1 
		{
			A_FadeOut(0.125);
			A_SetScale(Scale.X * 1.3);
		}
		Wait;
	}
}

Class PowerStunned : EOA_Powerup
{
	Default
	{
		Powerup.Duration 88;
		+EOA_POWERUP.NOLIGHT //[DoomKrakken]: Only done so that the powerlight in the base class doesn't spawn in the first place. ;)
	}
	Override Void AttachToOwner(Actor Other)
	{
		If (!Other.bISMONSTER || (Other.bBOSS && Other.Default.Health >= 3000))
		{
			Destroy();
			Return;
		}
		ActiveSound = "StunBomb/Stun";
		Super.AttachToOwner(Other);
		Red = 0.1;
		Green = 0.8;
		Blue = 1.0;
		Int R, G, B;
		R = Int(Red * 255);
		G = Int(Green * 255);
		B = Int(Blue * 255);
		EOA_ActorExtensions.SpawnCustomDynamicLight(Other, Self.GetClassName(), DynamicLight.PointLight, (R, G, B), 32, 0, DYNAMICLIGHT.LF_ATTENUATE, -1, (0, 0, Owner.Height / 2));
		Other.bFLOAT = False;
		Other.A_SetSpeed(0);
	}
	Override Void DoEffect()
	{
		If (EffectTics < 0)
		{
			Destroy();
			Return;
		}
		EOA_ActorExtensions.SpawnNoiseMaker(Owner, ActiveSound, 30, CHANF_LOOPING|CHANF_OVERLAP|CHANF_LISTENERZ);
		If (!(GetAge() % 5))
		{
			Owner.A_SpawnItemEx("StunBombLightning", ZOfs: Owner.Height / 2, XVel: frandom(-3, 3), YVel: frandom(-3, 3), ZVel: frandom(-3, 3), Flags: SXF_NOCHECKPOSITION);
		}
		Owner.bFORCEPAIN = True;
		Owner.bNODAMAGE = True;
		Owner.A_DamageSelf(1, Flags: DMSS_FOILINVUL|DMSS_FOILBUDDHA|DMSS_NOFACTOR|DMSS_NOPROTECT);
		Owner.bNODAMAGE = Owner.Default.bNODAMAGE;
		Owner.bFORCEPAIN = Owner.Default.bFORCEPAIN;
		Powerup.DoEffect(); //[DoomKrakken]: Not Super, because it calls unnecessary stuff we can skip.
	}
	Override Bool HandlePickup(Inventory Item)
	{
		If (Item.GetClass() == GetClass())
		{
			Let Power = EOA_Powerup(Item);
			If (!Power.EffectTics)
			{
				Power.bPICKUPGOOD = True;
				Return True;
			}
			If (Power.bADDITIVETIME) 
			{
				EffectTics += Power.EffectTics / (1 + (Owner.Default.Health >= 1000)); //[DoomKrakken]: Tanky monsters such as Barons burn through this stuff twice as fast... so the effect tics will be halved.
				BlendColor = Power.BlendColor;
			}
			Else If (EffectTics > BLINKTHRESHOLD && !Power.bALWAYSPICKUP)
			{
				Return True;
			}
			Else If (Power.EffectTics > EffectTics)
			{
				EffectTics = Power.EffectTics / (1 + (Owner.Default.Health >= 1000));
				BlendColor = Power.BlendColor;
			}
			Power.bPICKUPGOOD = True;
			Return True;
		}
		Return False;
	}
	Override Void EndEffect()
	{
		If (Owner)
		{
			Owner.bNODAMAGE = Owner.Default.bNODAMAGE;
			Owner.bFORCEPAIN = Owner.Default.bFORCEPAIN;
			Owner.bFLOAT = Owner.Default.bFLOAT;
			Owner.A_SetSpeed(Owner.Default.Speed);
		}
		Super.EndEffect();
	}
}

Class StunBombShockWave : Actor
{
	Default
	{
		RenderStyle "Add";
		Scale 0.05;
		+DONTSPLASH
		+FORCEXYBILLBOARD
		+NOINTERACTION
		+NOTRIGGER
		+ROLLCENTER
		+ROLLSPRITE
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetRoll(random(0, 359));
		BSW2 A 1 Bright
		{
			A_FadeOut(0.1);
			A_SetScale(Scale.X + 0.05 * (1 + CountInv("BigShock", AAPTR_TARGET)));
		}
		Wait;
	}
}

Class StunBombLightning : PlasmaImpactLightning
{
	Default
	{
		Scale 0.1;
		Translation "Cyan";
		+BRIGHT
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_SetRoll(random(0, 359));
			Return A_Jump(256, random(1, 8));
		}
		LBLT ABCDEFGH 0 A_Jump(256, "KeepSprite");
	KeepSprite:
		LBLT "#" 1 
		{
			A_FadeOut(0.1);
			A_SetScale(Scale.X * 1.1);
		}
		Wait;
	}
}

Class ChainStunTracerRetriever : Actor
{
	Action Bool EOA_ChainStun(Int Dist, Double Ang)
	{
		Let ChainStunOwner = Target;
		If (ChainStunOwner)
		{
			Tracer = Null;
			A_CopyFriendliness(AAPTR_MASTER);
			A_ClearTarget();
			A_LookEx(LOF_NOSOUNDCHECK|LOF_NOJUMP, 0, Dist, 0, Ang);
			If (Target && CheckIfTargetInLOS(Ang, JLOSF_ALLYNOJUMP, Dist) && Invoker.Distance3D(Target) <= Dist)
			{
				Tracer = Target;
				Target = ChainStunOwner;
				If (Tracer && (!Tracer.bBOSS || (Tracer.bBOSS && Tracer.Default.Health < 3000)))
				{
					A_GiveInventory("PowerStunned", 1, AAPTR_TRACER);
					Let StunItem = PowerStunned(Tracer.FindInventory("PowerStunned"));
					If (StunItem)
					{
						StunItem.EffectTics *= 1 + CountInv("LongerStun", AAPTR_MASTER);
						StunItem.EffectTics = clamp(StunItem.EffectTics, 0, 88 * (1 + CountInv("LongerStun", AAPTR_MASTER)));
					}
				}
				Return True;
			}
			Target = ChainStunOwner;
		}
		Return False;
	}
}

Class ChainStunLightningTracerSpawner : ChainStunTracerRetriever
{
	Default
	{
		Projectile;
		Radius 1;
		Radius 1;
		ReactionTime 12;
		MissileHeight 0;
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_RearrangePointers(AAPTR_MASTER, AAPTR_TARGET);
	See:
		TNT1 A 0 
		{
			If (EOA_ChainStun(400, 30))
			{
				A_SpawnItemEx("StunBombLightningTracerSpawner", Flags: SXF_TRANSFERPOINTERS);
			}
		}
	Turnito:
		TNT1 A 0
		{
			A_SetAngle(Angle + 30);
			A_CountDown();
		}
		Goto See;
	}
}

Class StunBombLightningTracerSpawner : Actor
{
	Default
	{
		Radius 1;
		Height 1;
		+NOINTERACTION
	}
	States
	{
	Spawn:
		TNT1 AAAAAAAA 1
		{
			If (Target)
			{
				A_Warp(AAPTR_TARGET, 0, 0, Target.Default.Height / 2, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE, Null);
				A_SpawnItemEx("StunBombLightningTracer", Flags: SXF_TRANSFERPOINTERS);
			}
		}
		Stop;
	}
}

Class StunBombLightningTracer : LightningBeamBase
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay 
		{
			If (!Tracer)
			{
				Return ResolveState("Null");
			}
			Dist[1] = Int(Distance3D(Tracer));
			Stats[0] = 64;
			Stats[1] = 400;
			Stats[2] = 8;
			Return ResolveState("Fly");
		}
		Goto Fly;
	Missile:
		TNT1 A 0
		{
			A_SpawnItemEx("StunBombLightningTracerFX", cos(-Pitch) * (Stats[0] / 2), 0, sin(Pitch) * (Stats[0] / 2), cos(-Pitch) * (Stats[0] * 0.0001), 0, sin(Pitch) * (Stats[0] * 0.0001), 0, SXF_TRANSFERPITCH);
			Return A_Jump(256, "Move");
		}
		Goto Move;
	}
}

Class StunBombLightningTracerFX : LightningBeamTrailBase
{
	Default
	{
		Translation "0:255=%[0, 0, 0]:[1.07, 1.65, 2]";
	}
}

//Red Plasma Ball, fired by enemies...
Class EOA_EnemyPlasmaBall : EOA_PlasmaBall
{
	Default
	{
		DamageFunction 5;
		RenderStyle "Add";
		Speed 30;
		Decal "EMGPuffBigScorchRed";
		Translation "0:255=%[0, 0, 0]:[1.5, 0.66, 0.66]";
		EOA_Projectile.PuffType "EOA_EnemyPlasmaPuffComplex";
	}
	States
	{
	Spawn:
		PSB1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PointLight, (178, 84, 84), 16, 16, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, 1, (0, 0, Height / 2));
		PSB2 ABCDEF 1 EOA_SpawnCustomDynamicLight("PLR_Light", DynamicLight.PointLight, (178, 84, 84), 16, 16, DYNAMICLIGHT.LF_ADDITIVE|DYNAMICLIGHT.LF_ATTENUATE, 1, (0, 0, Height / 2));
		Loop;
	XDeath:
		TNT1 A 0 A_StartSound("Projectile/HitFlesh");
	Crash:
	Death:
		TNT1 A 1
		{
			A_SpawnItemEx("EOA_EnemyPlasmaPuffComplex");
			A_StartSound("PlasmaBall/Impact", 5, 0, 0.25);
		}
		Stop;
	}
}

Class EOA_EnemyPlasmaPuff : EOA_PlasmaPuff //Graphics taken from D4T and ZION. :D
{
	Default
	{
		Translation "0:255=%[0, 0, 0]:[1.5, 0.66, 0.66]";
	}
}

Class EOA_EnemyPlasmaPuffComplex : EOA_EnemyPlasmaPuff
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_SpawnItemEx("EnemyPlasmaShockwave", Flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("EOA_EnemyPlasmaPuff", Flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("EnemyPlasmaImpactLightning", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), Flags: SXF_NOCHECKPOSITION);
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "PLRAddLight", DynamicLight.PulseLight, (178, 84, 84), 32, 1, DYNAMICLIGHT.LF_ADDITIVE, 3, (0, 0, Height / 2), 1.0 * 3 / TICRATE);
			EOA_ActorExtensions.SpawnCustomDynamicLight(Self, "PLRAttLight", DynamicLight.PulseLight, (178, 84, 84), 32, 1, DYNAMICLIGHT.LF_ATTENUATE, 3, (0, 0, Height / 2), 1.0 * 3 / TICRATE);
		}
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnProjectile("EnemyPlasmaSpark", 0, 0, random(1, 360), CMF_AIMDIRECTION, random(-45, 45));
			A_SpawnProjectile("RedPlasmaPuffSpark", 0, 0, random(1, 360), CMF_AIMDIRECTION, random(-90, 45));
		}
		Goto Super::Spawn;
	}
}

//[DoomKrakken]: Effects pilfered from ZION... :D
Class EnemyPlasmaShockWave : PlasmaShockWave
{
	Default
	{
		Translation "0:255=%[0, 0, 0]:[1.5, 0.66, 0.66]";
	}
}

Class EnemyPlasmaImpactLightning : PlasmaImpactLightning
{
	Default
	{
		Translation "Red";
	}
}

Class EnemyPlasmaSpark : PlasmaSpark
{
	Default
	{
		Translation "Red";
	}
}